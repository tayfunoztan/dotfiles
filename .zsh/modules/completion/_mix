#compdef mix

_arguments \
  '1:task names:->tasks' \
  '*:task arguments:->arguments'

local candidates

case $state in
  tasks)
    candidates=(${(f)"$(mix help | tail -n+2 | grep '^mix ' | cut -b5-)"})
    candidates=(${candidates/ *\# /:})
    _describe 'tasks' candidates
    return
esac

case $words[2] in
  archive.install)
    case $words[3] in
      hex)
        if [[ -z $words[4] ]]; then
          _message 'enter query for searching remote hex packges'
        else
          candidates=(${(f)"$(mix hex.search $words[4] | tail -n+2 | sed 's/  http.*$//')"})
          candidates=(${candidates/[[:space:]]##/:})
          _describe 'remote hex archives' candidates
        fi
        ;;
    esac
    ;;
  archive.uninstall)
    candidates=(${(f)"$(mix archive | grep '^* ' | cut -b3-)"})
    _describe 'local archives' candidates
    ;;
  help)
    candidates=(${(f)"$(mix help | tail -n+2 | grep '^mix ' | cut -b5-)"})
    candidates=(${candidates/ *\# /:})
    _describe 'tasks' candidates
    ;;
  *)
    _files
    ;;
esac
